buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'scala'
apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.minecraft_version + "-" + config.mod_version
group= "com.seremis.geninfusion" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "geninfusion"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"
    mappings = "snapshot_" + config.mcp_version
	
	replaceIn "GeneticInfusion.scala"
    replace "@MOD_VERSION@", project.config.mod_version
	replace "@MC_VERSION@", project.config.minecraft_version
}

dependencies {
    compile 'org.apache.commons:commons-math3:3.2'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task fixMCPNames(type: JavaExec, dependsOn: setupDecompWorkspace) {

	//logger.info(project.config.mcp_version)
	//logger.info(project.version)
	
	main = 'com.seremis.mcpNameFixer.Start'
	classpath = files('lib/MCPNameFixer.jar')
	workingDir = 'lib'
	systemProperties = [
		'mcpVersion': project.config.mcp_version,
		'forgeVersion': project.config.minecraft_version + "-" + project.config.forge_version
	]		
}
